# The MIT License (MIT)
#
# Copyright (c) 2016-2022 Objectionary.com
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+alias org.eolang.hamcrest.assert-that
+alias org.eolang.sys.call
+alias org.eolang.threads.thread
+alias org.eolang.threads.sleep
+architect 28lev11@gmail.com
+home https://github.com/objectionary/eo-threads
+junit
+package org.eolang.threads
+version 0.0.0

[] > thread-returns-string
  thread "hello" > t
  seq > @
    t.start
    QQ.io.stdout
      QQ.txt.sprintf
        "t was started. You must wait after me\n"
    eq.
      "hello"
      join.
        t

[] > thread-returns-number
  thread 7 > t
  seq > @
    t.start
    QQ.io.stdout
      QQ.txt.sprintf
        "t was started. You must wait after me\n"
    eq.
      7
      join.
        t

[] > complex-argument
  thread > t
    5.plus
      5
  seq > @
    t.start
    eq.
      10
      join.
        t

[] > multiple
  QQ.threads.thread > a
    2.plus 2
  QQ.threads.thread > b
    1.plus 1
  seq > @
    a.start
    b.start
    eq.
      times.
        a.join
        b.join
      8

[] > divide-by-zero-in-thread
  thread > t
    1.div 0
  assert-that > @
    seq
      t.start
      try
        []
          t.join > @
        [e]
          e > @
        nop
    $.equal-to
      "The 1th argument of 'int.div' is invalid: division by zero is infinity"

[] > parallel-sleep
  thread > t1
    sleep 200
  thread > t2
    sleep 200
  memory 0 > start
  memory 0 > finish
  assert-that > @
    seq
      start.write
        call "gettimeofday"
      t1.start
      t2.start
      t1.join
      t2.join
      finish.write
        call "gettimeofday"
      lt.
        finish.minus start
        390000
    $.equal-to TRUE
